module Siko.Parser where

import Siko.Syntax.Program
import Siko.Syntax.Location
import Siko.Parser.Token

data LexerError = InvalidEscapeSequence deriving (Show)

data LexerState =
    {
        chars :: [Char],
        index :: Int,
        line_offset :: Int,
        line_index :: Int,
        tokens :: [TokenInfo],
        errors :: [LexerError],
    }

emptyState cs = LexerState cs 0 0 0 [] []

isDone :: LexerState -> Bool
isDone state = state.index >= getLength state.chars

peek :: LexerState -> Char
peek state = atIndex state.chars state.index

step :: LexerState -> LexerState
step state = do
    c <- peek state
    (line_offset, line_index) <- if c == '\n' then (0, state.line_index + 1) else (state.line_offset + 1, state.line_index)
    state { index = state.index + 1, line_offset = line_offset, line_index = line_index }

startSpan :: LexerState -> Span
startSpan state = Span (Position state.line_index state.line_offset) (Position state.line_index state.line_offset)

updateSpan :: LexerState -> Span -> Span
updateSpan state span = span { end = (Position state.line_index state.line_offset) }

addToken :: LexerState -> Token -> Span -> LexerState
addToken state token span = do
    state { tokens = state.tokens + [TokenInfo token span] }

addError :: LexerState -> LexerError -> LexerState
addError state error = state { errors = state.errors + [error] }

isIdentifier c = case c of
    'a'..='z' | 'A'..='Z' | '0'..='9' | '_' -> True
    _ -> False

consumeWhile state cond = do
    current <- ""
    span <- startSpan state
    loop (state, (current, span)) \(state, (current, span)) ->
      do
        if not (isDone state)
            then
                do
                    c <- peek state
                    case c of
                        _ if cond c -> do
                            current <- current + "{}" % c
                            span <- updateSpan state span
                            ((step state, (current, span)), True)
                        _ -> ((state, (current, span)), False)
            else
                ((state, (current, span)), False)

consumeSingle :: LexerState -> Token -> LexerState
consumeSingle state token = do
    span <- startSpan state
    state <- step state
    addToken state token span

consumeString :: LexerState -> LexerState
consumeString state = do
    current <- ""
    state <- step state
    span <- startSpan state
    (state, (current, span)) <- loop (state, (current, span)) \(state, (current, span)) ->
      do
        if not (isDone state)
            then
                do
                    c <- peek state
                    case c of
                        '"' -> do
                            state <- step state
                            ((state, (current, span)), False)
                        '\\' -> do
                            state <- step state
                            c <- peek state
                            c <- case c of
                                'n' -> Some '\n'
                                't' -> Some '\t'
                                'r' -> Some '\r'
                                '"' -> Some '"'
                                _ -> None
                            (current, state) <- case c of
                                Some c -> do
                                    current <- current + "{}" % c
                                    (current, state)
                                None -> do
                                    state <- addError state InvalidEscapeSequence
                                    (current, state)
                            span <- updateSpan state span
                            ((step state, (current, span)), True)
                        _  -> do
                            current <- current + "{}" % c
                            span <- updateSpan state span
                            ((step state, (current, span)), True)
            else
                ((state, (current, span)), False)
    addToken state (StringLiteral current) span

parse :: String -> Program
parse input = 
    do
        cs <- String.chars input
        state <- emptyState cs
        state <- loop state \state -> if not (isDone state)
            then
                do
                    c <- peek state
                    state <- case c of
                        _ if isIdentifier c -> do
                            (state, (current, span)) <- consumeWhile state isIdentifier
                            addToken state (Identifier current) span
                        '"' -> consumeString state
                        '=' -> consumeSingle state Equal
                        '(' -> consumeSingle state LParen
                        ')' -> consumeSingle state RParen
                        '\n' -> step state
                        _ -> step state
                    (state, True)
            else
                (state, False)
        println "Tokens: {}" % state.tokens
        println "Errors: {}" % state.errors
        Program []
